/*
 * This software is in the public domain under CC0 1.0 Universal plus a
 * Grant of Patent License.
 *
 * To the extent possible under law, the author(s) have dedicated all
 * copyright and related and neighboring rights to this software to the
 * public domain worldwide. This software is distributed without any
 * warranty.
 *
 * You should have received a copy of the CC0 Public Domain Dedication
 * along with this software (see the LICENSE.md file). If not, see
 * <http://creativecommons.org/publicdomain/zero/1.0/>.
 */
apply plugin: 'java-library'
apply plugin: 'groovy'

def componentNode = parseComponent(project)
version = componentNode.'@version'
def jarBaseName = componentNode.'@name'
def moquiDir = projectDir.parentFile.parentFile.parentFile
def frameworkDir = file(moquiDir.absolutePath + '/framework')

// to run use "gradle dependencyUpdates"
apply plugin: 'com.github.ben-manes.versions'
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies { classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0' }
}
dependencyUpdates.resolutionStrategy = { componentSelection { rules -> rules.all { ComponentSelection selection ->
    boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/ }
    if (rejected) selection.reject('Release candidate')
} } }

repositories {
    flatDir name: 'localLib', dirs: frameworkDir.absolutePath + '/lib'
    flatDir name: 'librepo', dirs: projectDir.absolutePath + '/librepo'
    mavenCentral()
    //maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    api project(':framework')
    testImplementation project(':framework').configurations.testImplementation.allDependencies

    // also required in moqui-fop
    api ('org.apache.xmlgraphics:fop-core:2.7') {
        exclude group: 'xalan'
        exclude group: 'xml-apis' // can't exclude batik-ext but has some same classes as xml-apis so exclude that
        exclude group: 'org.apache.ant'
        exclude group: 'javax.servlet'
        exclude group: 'javax.media'
        exclude group: 'com.sun.media'
    }

    // Transbank
    //compile 'com.github.transbankdevelopers:transbank-sdk-java:1.5.1'

    // DTE (CÃ³digo PDF-417), also required in moqui-fop
    api module('net.sf.barcode4j:barcode4j:2.1')
    api module('net.sf.barcode4j:barcode4j-fop-ext:2.1')
    api 'uk.org.okapibarcode:okapibarcode:0.3.0'

    // XML SOAP (RemoteXmlsoapServiceRunner)
    api 'javax.xml.soap:javax.xml.soap-api:1.4.0'
    api 'com.sun.xml.messaging.saaj:saaj-impl:1.4.0'

    api 'xalan:xalan:2.7.0'

    api 'org.apache.httpcomponents:httpclient:4.5.13'
    api 'org.apache.httpcomponents:httpcore:4.4.14'
    api 'org.apache.httpcomponents:httpmime:4.5.3'
    api 'org.apache.xmlbeans:xmlbeans:2.6.0'

}

task cleanLib(type: Delete) { delete fileTree(dir: projectDir.absolutePath+'/lib', include: '*') }
clean.dependsOn cleanLib

task copyDependencies { doLast {
    copy { from (configurations.runtimeClasspath - project(':framework').configurations.runtimeClasspath - project(':framework').jar.archivePath)
        into file(projectDir.absolutePath + '/lib') }
} }
copyDependencies.dependsOn cleanLib
jar.dependsOn copyDependencies

check.dependsOn.clear()

jar {
    destinationDir = file(projectDir.absolutePath + '/lib')
    baseName = jarBaseName
}

test {
    dependsOn cleanTest
    include '**/*Tests.class'
    include '**/*BasicFlow.class'

    systemProperty 'moqui.runtime', moquiDir.absolutePath + '/runtime'
    systemProperty 'moqui.conf', 'conf/MoquiDevConf.xml'
    systemProperty 'moqui.init.static', 'true'
    maxHeapSize = "512M"

    testLogging.showStandardStreams = true

    classpath += files(sourceSets.main.output.classesDirs)
    // filter out classpath entries that don't exist (gradle adds a bunch of these), or ElasticSearch JarHell will blow up
    classpath = classpath.filter { it.exists() }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }
}
